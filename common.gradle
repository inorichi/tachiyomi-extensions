apply plugin: 'org.jmailen.kotlinter'

android {
    compileSdkVersion 27
    buildToolsVersion '29.0.3'

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    sourceSets {
        main {
            manifest.srcFile "$rootDir/AndroidManifest.xml"
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ["$rootDir/res"]
            assets.srcDirs = ['assets']
        }
        release {
            res.srcDirs = ['res']
            manifest.srcFile "AndroidManifest.xml"
        }
        debug {
            res.srcDirs = ['res']
            manifest.srcFile "AndroidManifest.xml"
        }
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        applicationIdSuffix pkgNameSuffix
        versionCode extVersionCode
        versionName "$libVersion.$extVersionCode"
        setProperty("archivesBaseName", "tachiyomi-$pkgNameSuffix-v$versionName")
        manifestPlaceholders = [
                appName : appName,
                extClass: extClass,
        ]
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Lib 1.2, but using specific commit so we don't need to bump up the version
    compileOnly "com.github.tachiyomiorg:extensions-lib:84bfb8d"

    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compileOnly 'com.squareup.okhttp3:okhttp:3.10.0'
    compileOnly 'io.reactivex:rxjava:1.3.6'
    compileOnly 'org.jsoup:jsoup:1.10.2'
}

preBuild.dependsOn(lintKotlin)
lintKotlin.dependsOn(formatKotlin)
